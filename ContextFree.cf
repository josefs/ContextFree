-- A grammar is list of statements

Grammar. Grammar ::= [Stmt] ;

-- Statements are either rules or a startrule directive

terminator Stmt "" ;

Rule.  Stmt ::= "rule" Ident Weight "{" [Call] "}" ;
Start. Stmt ::= "startshape" Ident ;

NoW. Weight ::= ;
A_W. Weight ::= Number ;

terminator Call "" ;

Call. Call ::= Ident "{" [Spec] "}" ;

terminator Spec "" ;

-- Here I'd like to be able to specify that I want any prefix of "size"
-- as the size specification. But maybe I can by creating a new token.

Y. Spec ::= "y" Number ;
X. Spec ::= "x" Number ;
Size. Spec ::= SizeTok Number ;
Rotate. Spec ::= "r" Number ;
Bright. Spec ::= "b" Number ;

comment "#";

token Number ('-'? digit+ ('.' digit+)?);
token SizeTok ('s' ('i' ('z' ('e')?)?)?);
